category Person {
    asset User {
        | userRights
            ->  os.accountDiscovery,
                os.applicationWindowDiscovery,
                os.automatedCollection,
                os.bruteForce,
                os.compileAfterDelivery,
                os.credentialsInFiles,
                os.domainDiscovery,
                os.downloadRemoteFile,
                os.directoryDiscovery,
                os.fileDiscovery,
                os.fileDeletion,
                os.filePermissionsModification,
                os.passwordPolicyDiscovery,
                os.permissionGroupsDiscovery,
                os.processInjection,
                os.runningProcessDiscovery,
                os.remoteSystemDiscovery,
                os.rootkit,
                os.scripting,
                os.systemInformationDiscovery,
                os.uploadRemoteFile, 
                os.userCommandLineInterface,
                os.userDataDestruction, 
                os.userDataEncryptedForImpact,
                os.userDiskWiped,
                windows.appCertDLLs,
                windows.bypassUserAccountControl,
                windows.changeDefaultFileAssociation,
                windows.cmstp,
                windows.compiledHTMLFile,
                windows.componentObjectModelHijacking,
                windows.controlPanelItems,
                windows.deobfuscateOrDecodeFilesOrInformation,
                windows.domainTrustDiscovery,
                windows.dynamicDataExchange,  
                windows.emailStorageCollection,
                windows.executeMshta,
                windows.logonScripts,
                windows.networkShareDiscovery,
                windows.peripheralDeviceDiscovery,
                windows.userSecuritySoftwareDiscovery,
                windows.videoCollection,
                linux.bash_profileAndBashrc,
                linux.spaceAfterFileName,
                linux.source,
                macOS.appleScript,
                macOS.bash_profileAndBashrc,
                macOS.logonScripts,
                macOS.spaceAfterFileName,
                macOS.networkShareDiscovery,
                macOS.userSecuritySoftwareDiscovery,
                macOS.videoCollection,
                macOS.source,
                computer.collectAudio,
                service.usertransmittedDataManipulation,
                service.remoteAccessTools,
                service._exploitationForDefenseEvasion,
                internalNetwork.usertransmittedDataManipulation

        | userCredentials
            ->  userRights,
                windows.outlookEmailCollection,
                internalNetwork.bypassAccessControl,
                internalNetwork.exchangeServerCollection,
                service.userRemoteServicesLogin

        | userInformation     
    }

    asset Administrator {
        | adminRights
            ->  user.userRights,
                createUserAccount,    
                os.automatedCollection,
                os.adminCommandLineInterface,
                os.adminDataDestruction,
                os.adminDataEncryptedForImpact,
                os.adminDiskWiped,
                os.credentialsInFiles,
                os.credentialsInRegistry,
                os.detailedRunningProcessDiscovery,
                os.detailedRemoteSystemDiscovery,
                os.diskStructureWipe,
                os.filePermissionsModification,                
                os.networkServiceScan,
                os.sensitiveDataCollected,
                windows.appCertDLLs,
                windows.appInitDLLs,
                windows.applicationShimming,
                windows.authenticationPackage,  
                windows.bootkit,
                windows.changeDefaultFileAssociation,
                windows.createService,
                windows.scheduledTask,
                linux.bash_profileAndBashrc,
                linux.bootkit,
                macOS.bash_profileAndBashrc,
                macOS.keychainCollection,
                service.admintransmittedDataManipulation,
                service.adminRemoteServicesLogin,
                service.applicationDeploymentSystems,
                internalNetwork.admintransmittedDataManipulation,
                internalNetwork.bypassAccessControl
             
        | adminCredentials
            ->  adminRights,
                service.adminRemoteServicesLogin,
                internalNetwork.bypassAccessControl
            
        & createUserAccount 
            ->  os.persistence          
        
        # multiFactorAuthentication
            info: "https://attack.mitre.org/mitigations/M1032/"
            ->  createUserAccount

        # privilegedAccountManagement
            info: "https://attack.mitre.org/mitigations/M1026/"
            ->  createUserAccount,
                windows.modifyRegistrySettings,
                windows.systemFirmware, 
                service.applicationDeploymentSystems
    }

    
    asset WindowsAdmin extends Administrator {
        | adminRights
            +>  accountManipulation,
                windowsWindowsAdmin.accessibilityFeatures,
                windowsWindowsAdmin.adminSecuritySoftwareDiscovery,
                windowsWindowsAdmin.bypassUserAccountControl,
                windowsWindowsAdmin.distributedComponentObjectModel,
                windowsWindowsAdmin.executeService,
                windowsWindowsAdmin.hooking,
                windowsWindowsAdmin.hypervisor,
                windowsWindowsAdmin.queryRegistery,
                windowsWindowsAdmin.systemFirmware

        & accountManipulation
            ->  user.userCredentials

        # multiFactorAuthentication
            +>  accountManipulation
    }

    
    asset Root extends Administrator {
        | rootRights
            ->  macOS.adminSecuritySoftwareDiscovery,
                os.rootDataDestruction,
                os.rootkit
    }
    
    asset SYSTEM {
        | systemRights
            ->  windows.appCertDLLs,
                windows.changeDefaultFileAssociation,
                windows.distributedComponentObjectModel,
                windows.hooking,
                windows.hypervisor,
                os.credentialsInFiles,
                os.filePermissionsModification,
                os.systemDataDestruction,
                os.rootkit

        | systemCredentials
            ->  systemRights
    }

    asset Employee {
        & spearphishingLinkClicked [UniformDistribution(20,1)]
            rationale: "20 % link https://faui1-files.cs.fau.de/filepool/publications/zina/2017-benenson-unpacking-spear-phishing.pdf"
            ->  browser.spearphishingLink

        | pageVisited
            ->  browser.maliciousWebsite

        | adLinkClicked
            ->  browser.maliciousAds

        & spearphishingAttachmentDownload
            -> browser.spearphishingAttachment

        # userTraining 
            info: "https://attack.mitre.org/mitigations/M1017/"        
            ->  spearphishingAttachmentDownload, 
                spearphishingLinkClicked, 
                userExecution,
                _mediaInserted,
                os._credentialsInFiles,   
                browser.runExtensions            
        
        & userExecution
            ->  computer.infectedComputer,
                computer.infectedWindowsComputer,
                os.scripting

        | insertMedia
            ->  userExecution,
                manualManipulation

        | manualManipulation
            ->  user.userRights

        | mediaInserted
            ->  _mediaInserted

        & _mediaInserted
            ->  removableMedia.dataExfiltrated      
    }
    
    asset Adversary {
        | maliciousWebsite
            ->  browser.maliciousWebsite, 
                employee.pageVisited

        | maliciousAds
            ->  browser.maliciousAds, 
                employee.adLinkClicked

        | maliciousCodeInserted
            ->  windows.executeCode         

        | spearphishingAttachmentAttack
            ->  employee.spearphishingAttachmentDownload,
                browser.spearphishingAttachment

        | spearphishingLinkAttack
            ->  employee.spearphishingLinkClicked,
                browser.spearphishingLink  
    }
}

category Software {
    asset Service {
        & applicationDeploymentSystems  

        & userRemoteServicesLogin

        & adminRemoteServicesLogin

        & usertransmittedDataManipulation
            info:"By manipulating transmitted data, adversaries may attempt to affect a business process, organizational understanding, and decision making. "

        & admintransmittedDataManipulation
            info:"By manipulating transmitted data, adversaries may attempt to affect a business process, organizational understanding, and decision making. "

        # encryptSensitiveInformation
            info:"https://attack.mitre.org/mitigations/M1041/"
            ->  usertransmittedDataManipulation,
                admintransmittedDataManipulation

        | remoteAccessTools
            -> internalNetwork.c2Connexion

        # executionPrevention
            info: "https://attack.mitre.org/mitigations/M1038/"
            ->  remoteAccessTools,
                userRemoteServicesLogin,
                adminRemoteServicesLogin

        | exploitationForDefenseEvasion // end step of securoty software discovery
            ->  _exploitationForDefenseEvasion

        & _exploitationForDefenseEvasion
            ->  os.bypassAntivirus,
                os.bypassSystemAccessControls

        # updateSoftware
            info: "https://attack.mitre.org/mitigations/M1051/"
            ->  windows.systemFirmware,
                exploitationForDefenseEvasion,
                internalNetwork.remoteAccess

        | informationRepositories
            -> os.dataCollected

        # applicationIsolationAndSandboxing
            info: "https://attack.mitre.org/mitigations/M1048/"
            ->  exploitationForDefenseEvasion,
                windows.distributedComponentObjectModel
    }

    asset VideoCallApplication extends Service { 
        | collectVideo
            ->  computer._collectVideo
    }
    
    abstractAsset OS {   
        & applicationDeploymentSoftware
            info:"Access to a network-wide or enterprise-wide software deployment system enables an adversary to have remote code execution on all systems that are connected to such a system."
            ->  executeCode

        | infectedOS
            ->  clipboardData,
                dataFromInformationRepositories,
                computer.infectedComputer

        & clipboardData
            ->  administrator.adminCredentials,
                user.userCredentials

        & accountDiscovery
            info: "get account names and information"

        & domainDiscovery
            info: "get domain names and information"

        | compromisedSystems //with probability
            ->  removableMedia.infectedMedia
            
        & binaryPadding
            ->  bypassSignatureBasedDetection,
                bypassAntivirus

        & applicationWindowDiscovery
            info: "get a listing of opened application windows"

        & runningProcessDiscovery
            info: "Adversaries may attempt to get information about running processes on a system. Information obtained could be used to gain an understanding of common software running on systems within the network."
            ->  processInjection

        & detailedRunningProcessDiscovery
            info: "Adversaries with admin rights may get better process ownership details"

        & commonlyUsedPortsConnection
            ->  c2Server.c2Connected,
                externalNetwork.bypassFirewall,
                internalNetwork.bypassFirewall,
                externalNetwork.bypassNetworkDetection,
                internalNetwork.bypassNetworkDetection

        | compileAfterDelivery     
            info:"Adversaries may attempt to make payloads difficult to discover and analyze by delivering files to victims as uncompiled code. Source code payloads may also be encrypted, encoded, and/or embedded within other files, such as those delivered as a Spearphishing Attachment."
            ->  adversary.spearphishingAttachmentAttack,
                bypassStaticFileAnalysis,
                bypassBinaryAnalysis,
                bypassAntivirus,
                bypassHostIntrusionPrevention,
                bypassSignatureBasedDetection

        & connectionProxy
            info:"Adversaries could use trust relationships to manage command and control communications, to reduce the number of simultaneous outbound network connections, to provide resiliency in the face of connection loss, or to ride over existing trusted communications paths between victims to avoid suspicion."
            -> c2Server.c2Connected           

        & customCryptographicProtocol
            info:"AAdversaries may use a custom cryptographic protocol or algorithm to hide command and control traffic."
            ->  c2Server.c2Connected        

        & customCommandAndControlProtocol
            info:"Adversaries may communicate using a custom command and control protocol instead of encapsulating commands/data in an existing Standard Application Layer Protocol."
            ->  c2Server.c2Connected          
           
        | disablingSecurityTools
            info: "Adversaries may disable security tools to avoid possible detection of their tools and activities."
            ->  bypassFileMonitoring,
                bypassHostIntrusionPrevention,
                bypassSignatureBasedDetection,
                bypassLogAnalysis,
                bypassAntivirus

        | credentialsInFiles
            ->  _credentialsInFiles

        & _credentialsInFiles
            ->  user.userCredentials,
                administrator.adminCredentials,
                system.systemCredentials         
               
        | executeCode 
            ->  internalNetwork.remoteAccess

        & obfuscatedFilesOrInformation
            info:" Adversaries may use Obfuscated Files or Information to hide artifacts of an intrusion from analysis."     
            ->  windows.deobfuscateOrDecodeFilesOrInformation, // leads to Deobfuscate/Decode Files or Information attack
                bypassHostForensicAnalysisDetection,
                bypassSignatureBasedDetection,
                bypassHostIntrusionPrevention,
                bypassApplicationWhitelisting,
                bypassProcessWhitelisting,
                bypassLogAnalysis,
                bypassFileOrPathWhitelisting          

        | runCode
            ->  computer.infectedComputer

        | replicationThroughRemovableMedia
            -> communicationThroughRemovableMedia

        & communicationThroughRemovableMedia

        & remoteSystemDiscovery
            info: "Adversaries will likely attempt to get a listing of other systems by IP address, hostname, or other logical identifier on a network that may be used for Lateral Movement from the current system."

        & detailedRemoteSystemDiscovery
            info: "Adversaries with admin rights may get better details of the hosts enumerated"
        
        & adminCommandLineInterface
            ->  dataCollected, // entrypoints of attacks that can be done with command line interface under admin rights
                obfuscatedFilesOrInformation // obfuscate commands executed from payloads or directly via a Command-Line Interface 
        
        & userCommandLineInterface
            ->  dataCollected, // entrypoints of attacks that can be done with command line interface under user rights
                obfuscatedFilesOrInformation // obfuscate commands executed from payloads or directly via a Command-Line Interface

        | filePermissionsModification
            info:"Adversaries may modify file permissions/attributes to evade intended DACLs."
            ->  windows.accessibilityFeatures, // leads to Accessibility Features attack
                windows.logonScripts, //leads to Logon Scripts attack
                macOS.logonScripts // leads to Logon Scripts attack

        & systemInformationDiscovery
            info:"An adversary may attempt to get detailed information about the operating system and hardware, including version, patches, hotfixes, service packs, and architecture."

        & processInjection
            ->  administrator.adminRights,
                bypassAntivirus,
                bypassProcessWhitelisting

        | rootkit
            info:"Rootkits are programs that hide the existence of malware by intercepting (i.e., Hooking) and modifying operating system API calls that supply system information."
            ->  bypassAntivirus,
                bypassFileMonitoring,
                bypassProcessWhitelisting,
                bypassSignatureBasedDetection, 
                bypassHostIntrusionPrevention,
                bypassSystemAccessControls,
                windows.hypervisor

        | modifyAPICalls                

        | bypassAntivirus

        | bypassBinaryAnalysis

        | bypassAutorunsAnalysis

        | bypassApplicationWhitelisting

        | bypassFileOrPathWhitelisting

        | bypassProcessWhitelisting

        | bypassSystemAccessControls
        
        | bypassFileMonitoring

        | bypassLogAnalysis        
        
        | bypassSignatureBasedDetection

        | bypassStaticFileAnalysis

        | bypassHostForensicAnalysisDetection
        
        | bypassHostIntrusionPrevention
        
        | bypassDigitalCertificateValidation   
        
        | passwordPolicyDiscovery
            ->  bruteForceWithPasswordPolicy

        & bruteForceWithPasswordPolicy
            ->  user.userCredentials // the probability distribution should be so that it has higher chance of success than the bruteforce attack

        & bruteForce
            info: "Adversaries may use brute force techniques to attempt access to accounts when passwords are unknown or when password hashes are obtained."
            ->  user.userCredentials           

        | codeDelivered
            ->  antivirusCheck

        & antivirusCheck
            ->  runCode

        & spearphishingAttachmentDownload
            ->  employee.userExecution

        & spearphishingLinkDownload
            ->  employee.userExecution

        | scripting
            ->  _scripting

        | persistence

        & _scripting
            -> automatedCollection
            
        # antivirus
            ->  antivirusCheck,
                obfuscatedFilesOrInformation,
                spearphishingAttachmentDownload,
                spearphishingLinkDownload   

        # restrictFileAndDirectoryPermissions 
            info: "https://attack.mitre.org/mitigations/M1022/"
            ->  _credentialsInFiles,
                windows.controlPanelItems

        # accountUsePolicies // Set account lockout policies after a certain number of failed login attempts to prevent passwords from being guessed. Too strict a policy may create a denial of service condition and render environments un-usable, with all accounts used in the brute force being locked-out.
            info: "https://attack.mitre.org/mitigations/M1036/" 
            ->  bruteForce

        # multiFactorAuthentication
            info: "https://attack.mitre.org/mitigations/M1032/" 
            ->  applicationDeploymentSoftware,
                bruteForce

        # passwordPolicies 
            info: "https://attack.mitre.org/mitigations/M1027/" 
            ->  bruteForce,
                _credentialsInFiles,
                _credentialsInRegistry

        # applicationIsolationAndSandboxing // Restrict execution of code to a virtual environment on or in transit to an endpoint system.
            info: "https://attack.mitre.org/mitigations/M1048/"
            ->  _scripting,
                browser.checkBrowser,
                browser.checkPlugin 

        # disableOrRemoveFeatureOrProgram 
            info: "https://attack.mitre.org/techniques/T1064/"
            ->  _scripting,
                communicationThroughRemovableMedia

        # activeDirectoryConfiguration
            info: "https://attack.mitre.org/mitigations/M1015/"
            ->  _credentialsInFiles

        # audit 
            info: "https://attack.mitre.org/mitigations/M1047/"
            ->  _credentialsInFiles,
                _credentialsInRegistry,
                browser.runExtensions  

        # codeSigning
            info: "https://attack.mitre.org/mitigations/M1045/" 
            ->  applicationDeploymentSoftware           

        # operatingSystemConfiguration 
            info: "https://attack.mitre.org/mitigations/M1028/"
            ->  accountDiscovery, 
                communicationThroughRemovableMedia,
                domainDiscovery,
                administrator.createUserAccount

        # executionPrevention // Block execution of code on a system through application whitelisting, blacklisting, and/or script blocking.            
            info: "https://attack.mitre.org/mitigations/M1038/"
            ->  accountDiscovery,
                binaryPadding,
                browser.runExtensions,
                domainDiscovery,
                applicationWindowDiscovery,
                runningProcessDiscovery,
                detailedRunningProcessDiscovery,
                remoteSystemDiscovery,
                detailedRemoteSystemDiscovery,
                adminCommandLineInterface, 
                userCommandLineInterface,
                processInjection,
                systemInformationDiscovery,
                fileDeletion, 
                dataCompressed, 
                computer.collectAudio,
                computer._collectVideo,
                _dataEncrypted,
                permissionGroupsDiscovery,
                _dataStaged,
                adminDiskWiped,
                windows.compiledHTMLFile
             
        # privilegedAccountManagement  
            info: "https://attack.mitre.org/mitigations/M1026/"
            ->  _credentialsInRegistry  

        | credentialsInRegistry
            ->  _credentialsInRegistry

        & _credentialsInRegistry
            ->  user.userCredentials,
                administrator.adminCredentials 

        & fileDeletion
            info: "Adversaries may remove traces (files) over the course of an intrusion to keep their footprint low or remove them at the end as part of the post-intrusion cleanup process"
            ->  bypassHostForensicAnalysisDetection

        & fileDiscovery
            ->  automatedCollection
        
        & diskStructureWipe
            info:"Adversaries may corrupt or wipe the disk data structures on hard drive necessary to boot systems; targeting specific critical systems as well as a large number of systems in a network to interrupt availability to system and network resources. "
 
        # dataBackup
            info: "https://attack.mitre.org/mitigations/M1053/"
            info:"Ensure backups are stored off system and is protected from common methods adversaries may use to gain access and destroy the backups to prevent recovery."
            ->  userDataDestruction,
                adminDataDestruction,
                rootDataDestruction,
                systemDataDestruction,
                userDataEncryptedForImpact, 
                adminDataEncryptedForImpact,
                diskStructureWipe,
                userDiskWiped,
                adminDiskWiped

        & userDiskWiped
            info:"Instead of wiping specific disk structures or files, adversaries with destructive intent may wipe arbitrary portions of disk content."

        & adminDiskWiped

        & uploadRemoteFile
            // leads to file execution attacks
            ->  employee.userExecution,
                automatedCollection  

        & downloadRemoteFile
            info: "data collection from the user"
        
        & sensitiveDataCollected
            ->  dataCompressed,
                computer.exfiltrationOverPhysicalMedium,
                dataSizedTransfer,
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2,
                automatedExfiltration,
                dataEncrypted, 
                dataStaged    

//        | dataCollected
//            ->  _dataCollected

        & dataCollected
            ->  dataCompressed,
                computer.exfiltrationOverPhysicalMedium,
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2,
                dataEncrypted,
                dataStaged,
                automatedExfiltration,
                dataSizedTransfer

        | dataEncrypted
            info: "Data is encrypted before being exfiltrated in order to hide the information that is being exfiltrated from detection or to make the exfiltration less conspicuous upon inspection by a defender."
            ->  _dataEncrypted

        & _dataEncrypted
            ->  computer.exfiltrationOverPhysicalMedium,
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2,
                automatedExfiltration,
                dataStaged 

        & _dataStaged // leads to exfiltration attacks
            -> computer.exfiltrationOverPhysicalMedium,
               exfiltrationOverAternativeProtocol,
               scheduledExfiltration,
               exfiltrationOverC2
        
        & userDataDestruction

        & adminDataDestruction

        & rootDataDestruction

        & systemDataDestruction
        
        & dataCompressed
            ->  dataEncrypted,
                dataStaged,
                automatedExfiltration,
                computer.exfiltrationOverPhysicalMedium,
                exfiltrationOverAternativeProtocol,
                scheduledExfiltration,
                exfiltrationOverC2

        | scheduledExfiltration
            ->  exfiltrationOverC2,
                exfiltrationOverAternativeProtocol
        
    
        | exfiltrationOverAternativeProtocol
            ->  internalNetwork.dataExfiltration

        & userDataEncryptedForImpact
            info: "Attackers can attempt to render stored data inaccessible by encrypting files or data on local and remote drives and withholding access to a decryption key. This may be done in order to extract monetary compensation from a victim in exchange for decryption or a decryption key (ransomware) or to render data permanently inaccessible in cases where the key is not saved or transmitted"

        & adminDataEncryptedForImpact

        | dataFromInformationRepositories 
            ->  service.informationRepositories

        | dataStaged    

        & directoryDiscovery
            ->  automatedCollection
          
        | automatedCollection
            ->  _automatedCollection


        & _automatedCollection
            ->  dataCollected, 
                sensitiveDataCollected

        | exfiltrationOverC2
            ->  c2Server.dataExfiltrated

        | automatedExfiltration
            ->  exfiltrationOverAternativeProtocol,
                exfiltrationOverC2
        
        | exfiltrationOverAternativeProtocol
            ->  internalNetwork.dataExfiltration

        # encryptSensitiveInformation 
            info: "https://attack.mitre.org/mitigations/M1041/"
            ->  _automatedCollection

        # remoteDataStorage 
            info: "https://attack.mitre.org/mitigations/M1029/"
            ->  _automatedCollection

        | dataSizedTransfer
            info: "An adversary may exfiltrate data in fixed size chunks instead of whole files or limit packet sizes below certain thresholds. This approach may be used to avoid triggering network data transfer threshold alerts."
            -> c2Server.dataExfiltrated                

        # limitSoftwareInstallation
            info: "https://attack.mitre.org/mitigations/M1033/"
            ->  browser.runExtensions,
                browser.compromisedBrowser 

        # updateSoftware
            info: "https://attack.mitre.org/mitigations/M1051/"
            ->  browser.runExtensions,
                browser.checkBrowser,
                browser.checkPlugin   

        # exploitProtection
            info: "https://attack.mitre.org/mitigations/M1050/"
            ->  browser.checkBrowser,
                browser.checkPlugin 

        & networkServiceScan
            info: "Adversaries may attempt to get a listing of services running on remote hosts, including those that may be vulnerable to remote software exploitation"

        & permissionGroupsDiscovery
            info: "Adversaries may attempt to find local system or domain-level groups and permissions settings. "

        | persistance

    }
    
    asset Windows extends OS {   
        | appCertDLLs
            ->  _appCertDLLs 
            
        & _appCertDLLs
            ->  persistence,
                systemWindows.systemRights,
                windowsUser.userRights,
                windowsAdministrator.adminRights

        & appInitDLLs 
            ->  persistence 

        & accessibilityFeatures
            info: "This helps attackers to open a System-level command shell, where the attackers can execute arbitrary Windows commands, including adding or modifying accounts on the system"
            ->  executeCode,
                system.systemRights,
                remoteDesktopProtocol 

        & cmstp
            info: "Microsoft Connection Manager Profile Installer (CMSTP.exe) is a command-line program used to install Connection Manager service profiles."
            ->  bypassUserAccountControl, // leads to Bypass User Account Control attack
                bypassApplicationWhitelisting,
                bypassAntivirus            

        & controlPanelItems
            info: "Adversaries can use Control Panel items as execution payloads to execute arbitrary commands."
            ->  executeCode,
                os.bypassApplicationWhitelisting,
                os.bypassProcessWhitelisting

        | componentObjectModelHijacking
            info:" Adversaries can use Component Object Model (COM) system to insert malicious code that can be executed. When that system component is executed through normal system operation the adversary's code will be executed instead."
            -> adversary.maliciousCodeInserted,
               bypassAutorunsAnalysis             

        | componentFirmware
            info: "Adversaries may compromise computer components and install malicious firmware that will execute adversary code outside of the operating system and main system firmware or BIOS."       
            ->  os.bypassFileMonitoring,
                os.bypassHostIntrusionPrevention, 
                os.bypassAntivirus         

        & compiledHTMLFile 
            info:"Adversaries may abuse this technology to conceal malicious code, can be triggered by User Execution."
            -> employee.userExecution, // leads to User Execution attack
               os.bypassApplicationWhitelisting,
               os.bypassDigitalCertificateValidation   

        | deobfuscateOrDecodeFilesOrInformation 
            ->  os.bypassAntivirus,
                os.bypassHostIntrusionPrevention,
                os.bypassSignatureBasedDetection,
                externalNetwork.bypassNetworkDetection,
                internalNetwork.bypassNetworkDetection              
               
        & executeMshta
            info: "Mshta is a utility that executes Microsoft HTML Applications. It can be used to proxy execute malicious .hta files, and can also be used to bypass application whitelisting."
            ->  executeCode,
                bypassDigitalCertificateValidation,
                bypassApplicationWhitelisting    
               
        | executeCode 
            +>  componentFirmware, 
                computer.infectedWindowsComputer          
        
        | remoteDesktopProtocol // leads to Remote Desktop Protocol attack
            ->  system.systemRights

        | rootkit
            info:"Rootkits are programs that hide the existence of malware by intercepting (i.e., Hooking) and modifying operating system API calls that supply system information."
            +>  hooking, // leads to hooking attack
                hypervisor, // leads to hypervisor attack
                systemFirmware // leads to system firmware attack

        | commandExecution
            ->  computer.infectedWindowsComputer

        & createService
            ->  executeService
            
        & registryKeysEnabled
            ->  dynamicDataExchange

        & distributedComponentObjectModel          
            info:"Through DCOM, adversaries operating in the context of an appropriately privileged user can remotely obtain arbitrary and even direct shellcode execution through Office applications as well as other Windows objects that contain insecure methods. DCOM can also execute macros and may also invoke Dynamic Data Exchange."
            ->  dynamicDataExchange

        | dynamicDataExchange
            info: "Windows Dynamic Data Exchange (DDE) is a client-server protocol for one-time and/or continuous inter-process communication (IPC) between applications."
            ->  userCommandLineInterface 
            
        | hooking             
            info:"Adversaries may use hooking to load and execute malicious code within the context of another process; malicious hooking mechanisms may also capture API calls that include parameters that reveal user authentication credentials for Credential Access."
            ->  executeCode,
                captureAPICalls

        | hypervisor           
            info:"A type-1 hypervisor operates at a level below the operating system and could be designed with Rootkit functionality to hide its existence from the guest operating system."
            -> persistance

        | captureAPICalls

        & modifyRegistrySettings
            ->  distributedComponentObjectModel      

        & logonScripts
            ->  windowsAdmin.adminRights   

        # restrictFileAndDirectoryPermissions
            info: "https://attack.mitre.org/mitigations/M1022/"
            +>  logonScripts   

        & systemFirmware
            info:"System firmware may be modified by an adversary to perform or assist in malicious activity."
            -> installMaliciousFirmwareUpdates

        | installMaliciousFirmwareUpdates              

        & queryRegistery
            info:"The Registry contains a significant amount of information about the operating system, configuration, software, and security. Some of the information may help adversaries to further their operation within a network."
            //-> os.systemInformationDiscovery,
               //computer.configuration,
              // software.installSoftware

        & networkShareDiscovery
            ->  internalNetwork.networkShareDiscovery

        & userCommandLineInterface
        
        & userSecuritySoftwareDiscovery
            info: "Adversaries may attempt to get a listing of security software, configurations, defensive tools, and sensors that are installed on the system. This may include things such as local firewall rules and anti-virus. These checks may be built into early-stage remote access tools."
            // -> leads to exploitation for defense evasion attack
        
        & adminSecuritySoftwareDiscovery
            //-> leads to exploitation for defense evasion attack

        & bypassUserAccountControl
            -> executeCode

        # executionPrevention             
            info: "https://attack.mitre.org/mitigations/M1038/"
            +>  accessibilityFeatures,
                adminSecuritySoftwareDiscovery,
                _appCertDLLs,
                appInitDLLs,
                cmstp,
                controlPanelItems,
                createService,
                executeMshta,
                networkShareDiscovery,
                queryRegistery,
                scheduledTask, 
                systemTimeDiscovery,
                userSecuritySoftwareDiscovery

        & executeService
            -> persistance
       
        | peripheralDeviceDiscovery
            -> computer.peripheralDeviceDiscovery

        & emailStorageCollection
            info:"Files containing email data can be acquired from a user's system, such as Outlook storage or cache files .pst and .ost."
            -> dataCollected

        & outlookEmailCollection
            info:"Some adversaries may acquire user credentials and access externally facing webmail applications, such as Outlook Web Access."
            -> dataCollected

        | dataCollected

        | hypervisor

        | systemFirmware   

        # updateSoftware
            info: "https://attack.mitre.org/mitigations/M1051/" 
            +>  appInitDLLs,
                _applicationShimming
        
        # userAccountControl
            info: "https://attack.mitre.org/mitigations/M1052/"
            +>  _applicationShimming, 
                bypassUserAccountControl 
        
        | applicationShimming
            ->  _applicationShimming
        
        & _applicationShimming
            ->  persistence,
                user.userRights,
                administrator.adminRights

        & authenticationPackage
            info: "Adversaries can use Windows Authentication Packages for persistence for persistence."
            ->  persistence   

        # privilegedProcessIntegrity  
            info: "https://attack.mitre.org/mitigations/M1025/"
            +>  authenticationPackage 

        # privilegedAccountManagement
            info: "https://attack.mitre.org/mitigations/M1026/"
            +>  bootkit,
                bypassUserAccountControl 

        | changeDefaultFileAssociation
            ->  persistence

        | trustedDomainInfo // leads to SID-History Injection, Pass the Ticket, and Kerberoasting attacks
            info: "The information discovered may help the adversary conduct SID-History Injection, Pass the Ticket, and Kerberoasting."

        & domainTrustDiscovery
            ->  trustedDomainInfo

        # audit 
            info: "https://attack.mitre.org/mitigations/M1047/"
            +>  domainTrustDiscovery,
                bypassUserAccountControl

        & bootkit 
            ->  persistance

        # bootIntegrity 
            info: "https://attack.mitre.org/mitigations/M1046/"
            +>  bootkit,
                systemFirmware      

        # encryptSensitiveInformation
            info: "https://attack.mitre.org/mitigations/M1041/"
            +>  emailStorageCollection,
                outlookEmailCollection

        # disableOrRemoveFeatureOrProgram
            info: "https://attack.mitre.org/mitigations/M1042/"
            +>  cmstp,
                distributedComponentObjectModel,
                registryKeysEnabled
            
        | videoCollection
            -> computer.collectVideo

        & scheduledTask
            info: "An adversary may use task scheduling to execute programs at system startup or on a scheduled basis for persistence, to conduct remote Execution as part of Lateral Movement, to gain SYSTEM privileges, or to run a process under the context of a specified account."
            //->  program execution attacks, scheduled persistance attacks, remote execution, gain system privilege, run process under context of specified account

        & systemTimeDiscovery
            info:"The information could be useful for performing other techniques, such as executing a file with a Scheduled Task [3], or to discover locality information based on time zone to assist in victim targeting."
            ->  scheduledTask
      
    }

    asset Linux extends OS {
        & bootkit
            ->  persistance

        # bootIntegrity 
            info: "https://attack.mitre.org/mitigations/M1046/"
            ->  bootkit

        | spaceAfterFileName
            ->  employee.userExecution
        
        //# fileMonitoring  This type of attack technique cannot be easily mitigated with preventive controls since it is based on the abuse of system features.
           // -> spaceAfterFileName

        | infectedOS
            +>  bashHistory

        | bash_profileAndBashrc
            -> _bash_profileAndBashrc

        & _bash_profileAndBashrc 
            ->  persistence

        & bashHistory 
            ->  linuxAdministrator.adminCredentials,
                linuxUser.userCredentials

        | source
            info:"The source command loads functions into the current shell or executes files in the current context."
            ->  executeCode

        | executeCode 
            +>  computer.infectedLinuxComputer
        //# fileMonitoring  This type of attack technique cannot be easily mitigated with preventive controls since it is based on the abuse of system features.
           // -> source            
            
        # restrictFileAndDirectoryPermissions
            info: "https://attack.mitre.org/mitigations/M1022/"
            ->  _bash_profileAndBashrc

        # operatingSystemConfiguration
            info: "https://attack.mitre.org/mitigations/M1028/" 
            ->  bashHistory

        # privilegedAccountManagement
            info: "https://attack.mitre.org/mitigations/M1026/"
            ->  bootkit          
    }

    asset MacOS extends OS {
        | infectedOS
            +>  bashHistory

        | videoCollection
            ->  computer.collectVideo

        | spaceAfterFileName
            ->  employee.userExecution
        
        //# fileMonitoring  This type of attack technique cannot be easily mitigated with preventive controls since it is based on the abuse of system features.
           // -> spaceAfterFileName

        & networkShareDiscovery
            ->  internalNetwork.networkShareDiscovery

        & userSecuritySoftwareDiscovery
            info: "Adversaries may attempt to get a listing of security software, configurations, defensive tools, and sensors that are installed on the system. This may include things such as local firewall rules and anti-virus. These checks may be built into early-stage remote access tools."
            // -> leads to exploitation for defense evasion attack

        & adminSecuritySoftwareDiscovery
            // -> leads to exploitation for defense evasion attack 

        # executionPrevention
            +>  networkShareDiscovery,
                userSecuritySoftwareDiscovery,
                adminSecuritySoftwareDiscovery

        & keychainCollection
            info: "If an adversary knows the credentials for the login keychain, then they can get access to all the other credentials stored in this vault."
            ->  user.userCredentials

        # passwordPolicies // Set and enforce secure password policies for accounts 
            info: "https://attack.mitre.org/mitigations/M1027/"
            +>  keychainCollection

        & appleScript
            ->  runCode

        | runCode
            ->  computer.infectedMacOSComputer

        # codeSigning
            info: "https://attack.mitre.org/mitigations/M1045/"  
            +>  appleScript

        | bash_profileAndBashrc
            ->  _bash_profileAndBashrc

        & _bash_profileAndBashrc 
            ->  persistence

        & logonScripts
            ->  rootMac.rootRights  

        | source

        | executeCode 
            +>  computer.infectedMacOSComputer

        //# fileMonitoring  This type of attack technique cannot be easily mitigated with preventive controls since it is based on the abuse of system features.
           // -> source     

        # restrictFileAndDirectoryPermissions
            info: "https://attack.mitre.org/mitigations/M1022/"
            +>  _bash_profileAndBashrc,
                logonScripts

        & bashHistory 
            ->  macosAdministrator.adminCredentials,
                macOSUser.userCredentials

        # operatingSystemConfiguration
            info: "https://attack.mitre.org/mitigations/M1028/" 
            +>  bashHistory
    }
    
}

category Network {
    asset Browser {
        | maliciousWebsite
            ->  checkPlugin,
                checkBrowser

        & checkBrowser
            ->  os.codeDelivered

        & checkPlugin
            ->  os.codeDelivered

        | maliciousAds
            ->  compromisedBrowser

        & compromisedBrowser
            ->  os.codeDelivered
        
        & spearphishingLink
            ->  os.spearphishingLinkDownload

        & spearphishingAttachment
            ->  os.spearphishingAttachmentDownload,
                windows.controlPanelItems // malicious Control Panel items can be delivered via Spearphishing Attachment campaigns

        # restrictWebBasedContent // Block unknown or unused attachments by default that should not be transmitted over email.
          info: "https://attack.mitre.org/mitigations/M1021/"
            ->  spearphishingAttachment, 
                spearphishingLink,
                checkBrowser,
                checkPlugin,
                windows.compiledHTMLFile

        | browserBookmark
            ->  browserBookmarkDiscovery

        | browserBookmarkDiscovery 
            ->  user.userInformation,
                internalNetwork.internalNetworkResourcesInformation

        | installExtensions
            ->  runExtensions

        & runExtensions
            ->  os.persistence
    }

    asset ExternalNetwork {
        # networkIntrusionPrevention
            info: "https://attack.mitre.org/mitigations/M1031/"
            ->  c2Server.obfuscatedData,
                os.connectionProxy

        | bypassNetworkDetection

        | dataExfiltrated

        | bypassFirewall
    }

    asset InternalNetwork {
        | remoteAccess

        & applicationLayerConnexion
            ->  c2Connexion,
                bypassNetworkDetection,
                externalNetwork.bypassNetworkDetection

        & dataExfiltration
            ->  externalNetwork.dataExfiltrated
        
        E firewallExists
            <-  firewall
            ->  firewall._bypassFirewall
               
        | bypassAccessControl
        
        | bypassNetworkDetection

        | bypassProxies

        & usertransmittedDataManipulation
            info:"By manipulating transmitted data, adversaries may attempt to affect a business process, organizational understanding, and decision making. "

        & admintransmittedDataManipulation
            info:"By manipulating transmitted data, adversaries may attempt to affect a business process, organizational understanding, and decision making. "

        # encryptSensitiveInformation
            info:"https://attack.mitre.org/mitigations/M1041/"   
            ->  usertransmittedDataManipulation,
                admintransmittedDataManipulation,
                exchangeServerCollection

        # executionPrevention             
            info: "https://attack.mitre.org/mitigations/M1038/"
            ->  os.networkServiceScan

        # filterNetworkTraffic
            info:"https://attack.mitre.org/mitigations/M1037/"   
            ->  os.customCommandAndControlProtocol 

        & c2Connexion
            ->  c2Server.c2Connected

        & exchangeServerCollection
            info:"Adversaries may leverage a user's credentials and interact directly with the Exchange server to acquire information from within a network."
            ->  os.dataCollected

        # multiFactorAuthentication
            info: "https://attack.mitre.org/mitigations/M1032/"
            ->  exchangeServerCollection

        | networkShareDiscovery
            info:"Networks often contain shared network drives and folders that enable users to access file directories on various systems across a network. "
        
        | internalNetworkResourcesInformation

        | bypassFirewall

        # networkIntrusionPrevention
            info: "https://attack.mitre.org/mitigations/M1031/"
            ->  applicationLayerConnexion,
                c2Connexion,
                dataExfiltration,
                os.commonlyUsedPortsConnection,
                os.connectionProxy,
                os.customCommandAndControlProtocol,
                os.customCryptographicProtocol,
                os.dataCompressed,
                os.downloadRemoteFile,
                os.networkServiceScan,
                os.uploadRemoteFile, 
                c2Server.dataExfiltrated,
                c2Server._obfuscatedData,
                c2Server.dataExfiltrated

        # networkSegmentation
            info: "https://attack.mitre.org/mitigations/M1030/"
            ->  remoteAccess,
                administrator.createUserAccount,
                os.commonlyUsedPortsConnection,
                os.customCommandAndControlProtocol,
                windows.domainTrustDiscovery

        | connectedToNetwork
            ->  networkSharedDrive.connectToDrive

    }

    asset Firewall {
        & _bypassFirewall
            ->  internalNetwork.dataExfiltration,
                internalNetwork.c2Connexion

        # networkSegmentation // Properly configure firewalls and proxies to limit outgoing traffic to only necessary ports for that particular network segment.
            info: "https://attack.mitre.org/mitigations/M1030/"
            ->  computer.uncommonlyUsedPortsConnection,
                windows.distributedComponentObjectModel
        
        | bypassFirewall
    }

    asset NetworkSharedDrive {
        | data
            ->  c2Server.obfuscatedData
            
        | connectToDrive
            ->  data
    }
}

category Hardware {
    asset Computer {
        | infectedComputer
            ->  removableMedia.infectedMedia,
                internalNetwork.applicationLayerConnexion,
                uncommonlyUsedPortsConnection,
                removableMedia.dataFromRemovableMedia

        | infectedWindowsComputer
            ->  windows.dynamicDataExchange

        | infectedLinuxComputer

        | infectedMacOSComputer

        & commonlyUsedPortsConnection
            ->  internalNetwork.c2Connexion,
                firewall.bypassFirewall,
                externalNetwork.bypassNetworkDetection,
                internalNetwork.bypassNetworkDetection

        & uncommonlyUsedPortsConnection
            ->  internalNetwork.c2Connexion,
                firewall.bypassFirewall,
                internalNetwork.bypassProxies       

        | exfiltrationOverPhysicalMedium
            ->  removableMedia.dataExfiltrated

        E microphoneExists
            <-  microphone
            ->  microphone.collectAudio

        & collectAudio
            ->  os.dataCollected
        
        | peripheralDeviceDiscovery
            info: "Adversaries may attempt to gather information about attached peripheral devices and components connected to a computer system. The information may be used to enhance their awareness of the system and network environment or may be used for further actions."

        E webcamExists
            <-  webcam
            ->  webcam.collectVideo

        E videoCallApplicationExists
            <-  videoCallApplication
            ->  videoCallApplication.collectVideo

        | collectVideo
            ->  _collectVideo

        & _collectVideo
            -> os.dataCollected
    }

    asset Webcam {
        | collectVideo
            ->  computer._collectVideo
    }

    asset C2Server {
        | c2Connected 
            ->  obfuscatedData,
                packetCapture

        | obfuscatedData
            ->  _obfuscatedData

        & _obfuscatedData
            ->  externalNetwork.bypassNetworkDetection,
                internalNetwork.bypassNetworkDetection,
                bypassNetworkDetection

        & dataExfiltrated

        | bypassNetworkDetection

        | packetCapture
    }

    asset RemovableMedia {
        | dataFromRemovableMedia
            ->  sensitiveDataCollected

        | infectedMedia
            ->  employee.insertMedia,
                os.replicationThroughRemovableMedia

        & dataExfiltrated

        | sensitiveDataCollected

    }
    
    asset Microphone {
        | collectAudio
            ->  computer.collectAudio
    }
}

associations {    
    
    Adversary [adversary] 1 <--Attack--> 1-* [employee] Employee 
    Adversary [adversary] 1 <--Attack--> 1-* [browser] Browser 
    Adversary [adversary] 1 <--Attack--> 1-* [os] OS 
    Adversary [adversary] 1 <-- Attack--> 1 [windows] Windows 

    User [user] 1 <--Ui--> 1-* [os] OS
    User [windowsUser] 1 <--Use--> 1-* [windows] Windows
    User [linuxUser] 1-* <--Use--> 1 [linux] Linux   
    User [macOSUser] 1-* <--Use--> 1 [macOS] MacOS
    User [user] 1-* <-- Ui--> 1 [computer] Computer
    User [user] 1-* <--Use--> 1-* [service] Service
  
    Administrator [administrator] 1 <--Ui--> 1-* [user] User
    Administrator [administrator] 1 <--Ui--> 1-* [os] OS
    Administrator [windowsAdministrator] 1 <--Ui--> 1-* [windows] Windows
    Administrator [linuxAdministrator] 1 <--Ui--> 1-* [linux] Linux
    Administrator [macosAdministrator] 1-* <--Ui--> 1-* [macOS] MacOS
    Administrator [administrator] 1 <--Ui--> 1-* [computer] Computer
    Administrator [administrator] 1-* <--Use--> 1-* [service] Service
    
    //WindowsAdmin [windowsAdmin] 1 <--Use--> 1 [os] OS
    WindowsAdmin [windowsAdmin] 1-* <--Ui--> 1 [windowsWindowsAdmin] Windows    
    //WindowsAdmin [windowsAdmin] 1 <--Use--> 1 [administrator] Administrator
    //WindowsAdmin [windowsAdmin] 1-* <--Ui--> 1 [windows] Windows  

    Root [rootMac] 1-* <--Use--> 1-* [macOS] MacOS

    SYSTEM [systemWindows] 1-* <--Ui--> 1-* [windows] Windows

    Employee [employee] 1 <--Use--> 1-* [browser] Browser
    Employee [employee] 1 <--Use--> 1-* [computer] Computer
    Employee [employee] 1 <--Use--> 1-* [user] User
    Employee [employee] 1 <--Use--> 1-* [os] OS
    Employee [employee] 1 <--Use--> 1-* [windows] Windows
    Employee [employee] 1 <--Use--> 1-* [macOS] MacOS
    Employee [employee] 1 <--Use--> 1-* [linux] Linux
    Employee [employee] 1 <--Use--> 1-* [removableMedia] RemovableMedia

    Computer [computer] 1 <--Use--> 1-* [removableMedia] RemovableMedia
    Computer [computer] 1 <--Control--> 1 [c2Server] C2Server

    Browser [browser] 1-* <--Software--> 1 [os] OS
    Browser [browser] 1-* <--Software--> 1 [windows] Windows
    Browser [browser] 1-* <--Ui--> 1-* [user] User
    Browser [browser] 1-* <--Ui--> 1-* [internalNetwork] InternalNetwork

    OS [os] 1 <-- System --> 1-* [computer] Computer
    OS [os] 1-* <--Ui--> 1 [system] SYSTEM 
    OS [os] 1  <--Contain--> 1-* [service] Service
    OS [os] 1-* <--Ui--> 1-* [c2Server] C2Server
    OS [os] 1 <--Contain--> 1 [windows] Windows
    OS [os] 1 <--Contain--> 1 [macOS] MacOS
    OS [os] 1 <--Use--> 1-* [removableMedia] RemovableMedia

    Service [service] 1-* <--Contain--> 1 [windows] Windows
    
    //Computer [computer] 1 <--Software--> 1 [macOS] MacOS
    Computer [computerWindows] 1 <--Software--> 1 [windows] Windows
    //Computer [computer] 1 <--Software--> 1 [linux] Linux //190820 added Source
    
    ExternalNetwork [externalNetwork] 1 <--Contain--> 1 [c2Server] C2Server
    ExternalNetwork [externalNetwork] 1 <--Contain--> 1-* [computer] Computer
    ExternalNetwork [externalNetwork] 1 <--Connect--> 1-* [os] OS

    InternalNetwork [internalNetwork] 1 <--Contain--> 1 [c2Server] C2Server
    InternalNetwork [internalNetwork] 1-* <--Connect--> 1-* [os] OS
    InternalNetwork [internalNetworkWindows] 1-* <--Contain--> 1-* [windows] Windows
    InternalNetwork [internalNetwork] 1 <--Protect--> 1 [firewall] Firewall
    InternalNetwork [internalNetwork] 1 <--Connect--> 1-* [externalNetwork] ExternalNetwork
    InternalNetwork [internalNetwork] 1 <--Contain--> 1-* [user] User
    InternalNetwork [internalNetwork] 1 <--Contain--> 1-* [administrator] Administrator
    InternalNetwork [internalNetwork] 1 <--Contain--> 1-* [computer] Computer
    //InternalNetwork [internalNetwork] 1 <--Contain--> 1-* [windows] Windows
    //InternalNetwork [internalNetwork] 1 <--Contain--> 1-* [macOS] MacOS
    InternalNetwork [internalNetwork] 1 <--Contain--> 1-* [service] Service
    InternalNetwork [internalNetwork] 1 <--Contain--> 1-* [networkSharedDrive] NetworkSharedDrive      
    NetworkSharedDrive [networkSharedDrive] 1-* <--Ui--> 1-* [c2Server] C2Server
    Windows [windows] 1 <--Protect--> 1 [firewall] Firewall
    Computer [computer] 1-* <--Protects--> 0-1 [firewall] Firewall
    Computer [computer] 1 <--Use--> 1 [microphone] Microphone
    VideoCallApplication [videoCallApplication] 1-* <--Software--> 1 [computer] Computer
    Webcam [webcam] 1 <--Hardware--> 1 [computer] Computer

}
